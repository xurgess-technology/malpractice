extends Node
## Windowed screenshots of the generated hospital: the neutral area, the entrance building, the
## break room, the OR, one of each wing room kind and the longest hallway.
##
##   godot --path . tools/hospitalshot.tscn --resolution 1280x720 -- [--seed=N] [--only=or,lab] [--light=off]
##
## Writes tools/hospital_shots/<seed>_<name>.png.

const OUT_DIR := "res://tools/hospital_shots"
const SETTLE_FRAMES := 30
const ROOM_KINDS := ["patient_room", "supply_closet", "pharmacy", "nurse_station", "waiting_room", "restroom",
		"office", "lab", "radiology", "morgue", "janitor_closet", "cafeteria"]

var main: Node3D
var game: Game
var bot: Player
var _seed := 4242
var _only: Array = []
var _flashlight := true


func _ready() -> void:
	for a in OS.get_cmdline_user_args():
		if a.begins_with("--seed="):
			_seed = int(a.split("=")[1])
		elif a.begins_with("--only="):
			_only = Array(a.split("=")[1].split(","))
		elif a == "--light=off":
			_flashlight = false
	DirAccess.make_dir_recursive_absolute(ProjectSettings.globalize_path(OUT_DIR))
	main = load("res://scenes/main.tscn").instantiate()
	add_child(main)
	await get_tree().process_frame
	if main.launching:
		await main.launched   # the launch printout covers the screen until then
	game = main.game
	main.menu.hide_menu()
	Net.start_solo("Camera")
	game.start_session(_seed)
	await get_tree().process_frame
	bot = game.local_player()
	bot.bot_active = true
	bot.bot_invulnerable = true
	bot.bot_move = Vector2.ZERO
	while game.get_parent().has_node("WarmupCover"):
		await get_tree().process_frame
	for i in 10:
		await get_tree().process_frame
	if bot.has_method("set_flashlight"):
		bot.set_flashlight(_flashlight)

	var shots: Array = [
		["neutral_lot", _pose_neutral],
		["neutral_doors", _pose_neutral_doors],
		["entrance_lobby", _pose_lobby],
		["entrance_hall", _pose_hall],
		["break_room", _pose_break_room],
		["or_tables", _pose_or],
		["hallway_long", _pose_hallway],
		# The hub rebuild (scripts/level/entrance.gd), from fixed tiles of the building's plan.
		["hub_lobby_in", _pose_hub.bind(Vector2(19.5, 31.0), Vector2(16.0, 19.0), 1.2)],
		["hub_spine", _pose_hub.bind(Vector2(16.5, 21.0), Vector2(16.5, 2.0), 1.4)],
		["hub_hallway", _pose_hub.bind(Vector2(2.0, 2.5), Vector2(31.0, 2.5), 1.4)],
		["hub_hallway_back", _pose_hub.bind(Vector2(31.0, 2.8), Vector2(1.0, 2.5), 1.2)],
		["hub_hallway_west", _pose_hub.bind(Vector2(14.0, 3.2), Vector2(4.0, 2.2), 0.4)],
		["hub_hallway_east", _pose_hub.bind(Vector2(18.5, 2.2), Vector2(28.0, 3.4), 0.4)],
		["hub_hall_bag", _pose_hub.bind(Vector2(7.2, 3.3), Vector2(5.0, 1.3), 0.5)],
		["hub_hall_drag", _pose_hub.bind(Vector2(12.5, 3.0), Vector2(8.5, 4.6), 0.1)],
		["hub_hall_toppled", _pose_hub.bind(Vector2(10.5, 3.6), Vector2(12.0, 1.6), 0.3)],
		["hub_hall_east_bodies", _pose_hub.bind(Vector2(22.5, 2.6), Vector2(27.8, 3.6), 0.6)],
		["hub_or", _pose_hub.bind(Vector2(13.0, 11.8), Vector2(5.5, 7.5), 0.9)],
		["hub_or_lab", _pose_hub.bind(Vector2(6.4, 10.6), Vector2(6.4, 13.0), 1.1)],
		["hub_or_lab_close", _pose_hub.bind(Vector2(10.3, 10.6), Vector2(10.3, 13.0), 1.0)],
		["hub_or_tables", _pose_hub.bind(Vector2(7.0, 11.5), Vector2(11.0, 6.5), 1.0)],
		["hub_storage", _pose_hub.bind(Vector2(6.0, 7.5), Vector2(1.5, 7.0), 1.0)],
		["hub_crematorium", _pose_hub.bind(Vector2(20.9, 9.0), Vector2(29.0, 9.0), 1.3)],
		["hub_furnace_open", _pose_furnace_open],
		["zz_a", _pose_hub.bind(Vector2(23.5, 9.0), Vector2(29.0, 10.6), 1.3)],
		["zz_c", _pose_hub.bind(Vector2(23.5, 9.0), Vector2(29.0, 9.0), 2.6)],
		["hub_crematorium_back", _pose_hub.bind(Vector2(27.6, 9.4), Vector2(19.0, 8.0), 1.0)],
		["hub_crematorium_junk", _pose_hub.bind(Vector2(23.0, 9.0), Vector2(20.0, 6.2), 0.6)],
		["hub_crematorium_barrow", _pose_hub.bind(Vector2(23.5, 9.0), Vector2(27.5, 12.6), 0.6)],
		["hub_crematorium_doors", _pose_hub.bind(Vector2(15.3, 12.5), Vector2(18.0, 9.0), 2.2)],
		["hub_break_room", _pose_hub.bind(Vector2(13.0, 17.8), Vector2(1.5, 14.8), 1.0)],
		["hub_personnel", _pose_hub.bind(Vector2(17.0, 16.5), Vector2(31.0, 16.5), 1.2)],
		["hub_personnel_back", _pose_hub.bind(Vector2(30.3, 16.5), Vector2(19.0, 16.8), 1.2)],
		["hub_personnel_lockers", _pose_hub.bind(Vector2(24.0, 18.4), Vector2(24.0, 14.0), 1.4)],
		["hub_personnel_sinks", _pose_hub.bind(Vector2(21.0, 15.2), Vector2(26.0, 18.9), 1.3)],
		["hub_personnel_machine", _pose_hub.bind(Vector2(24.5, 16.5), Vector2(32.0, 16.5), 1.6)],
		["hub_personnel_machine_close", _pose_hub.bind(Vector2(28.6, 15.6), Vector2(32.0, 16.9), 1.4)],
		["hub_personnel_mirror", _pose_hub.bind(Vector2(24.2, 16.2), Vector2(25.3, 19.0), 1.4)],
		["hub_personnel_showers", _pose_hub.bind(Vector2(25.5, 17.8), Vector2(30.5, 14.4), 1.2)],
		# SHOWERS (2026-09-24): the same spot, with the nearest shower's water turned on.
		["hub_personnel_shower_on", _pose_hub_shower_on],
		["hub_waiting", _pose_hub.bind(Vector2(14.0, 24.5), Vector2(1.5, 21.0), 1.0)],
		["hub_pharmacy", _pose_hub.bind(Vector2(18.5, 26.5), Vector2(23.5, 24.0), 1.4)],
		["hub_triage", _pose_hub.bind(Vector2(19.8, 25.8), Vector2(16.3, 23.8), 1.0)],
		["hub_fax_form", _pose_fax_form],
		["hub_drawer", _pose_drawer],
		["hub_nurse_reading", _pose_nurse_reading],
		["hub_fax_desk", _pose_fax_desk],
		["hub_lobby_desks", _pose_hub.bind(Vector2(15.6, 22.6), Vector2(14.0, 20.4), 0.8)],
		["hub_triage_desk", _pose_hub.bind(Vector2(14.2, 24.6), Vector2(16.4, 23.3), 0.9)],
		["hub_computer", _pose_hub.bind(Vector2(9.2, 16.4), Vector2(10.3, 18.9), 0.75)],
		["hub_projector", _pose_hub.bind(Vector2(4.6, 14.5), Vector2(10.0, 18.6), 1.2)],
		["hub_leak_waiting", _pose_hub_leak.bind(Vector2(6.0, 21.2), Vector2(9.0, 20.0), 1.8, true)],
		["hub_leak_personnel", _pose_hub_leak.bind(Vector2(27.0, 17.5), Vector2(28.5, 14.0), 1.5, false)],
		["hub_projector_behind", _pose_hub.bind(Vector2(10.0, 14.2), Vector2(10.0, 19.0), 1.4)],
		["hub_waiting_nurse", _pose_waiting_nurse.bind(Vector3(0.9, 0.5, -2.6))],
		["hub_waiting_nurse_side", _pose_waiting_nurse.bind(Vector3(-1.9, 0.3, -0.6))],
		["hub_waiting_nurse_close", _pose_waiting_nurse.bind(Vector3(-0.7, 0.0, -1.3))],
	]
	for k in ROOM_KINDS:
		shots.append(["room_" + k, _pose_room.bind(k)])
	for s in shots:
		if not _only.is_empty() and not _only.has(s[0]):
			continue
		var ok: bool = await s[1].call()
		if not ok:
			print("[hospitalshot] no pose for ", s[0])
			continue
		for i in SETTLE_FRAMES:
			await get_tree().process_frame
		var img := get_viewport().get_texture().get_image()
		var path := "%s/%d_%s.png" % [OUT_DIR, _seed, s[0]]
		img.save_png(ProjectSettings.globalize_path(path))
		print("[hospitalshot] wrote ", path)
	get_tree().quit(0)


func _look_from(pos: Vector3, at: Vector3) -> void:
	bot.teleport(pos)
	var d := at - (pos + Vector3.UP * C.EYE_H)
	bot.bot_yaw = atan2(-d.x, -d.z)
	bot._yaw = bot.bot_yaw
	bot.rotation.y = bot.bot_yaw
	bot.bot_pitch = clampf(atan2(d.y, Vector2(d.x, d.z).length()), -1.0, 1.0)
	bot._pitch = bot.bot_pitch
	bot.head.rotation.x = bot._pitch


func _pose_neutral() -> bool:
	var n: Dictionary = game.level_info.get("neutral", {})
	if n.is_empty() or (n.get("spawn_points", []) as Array).is_empty():
		return false
	# SWEEP 4A HOOK (pharmacy, chunk 3): the lot is empty asphalt and fog now; the gold pile spot
	# is gone, so anchor on a spawn point instead.
	var anchor: Vector3 = (n.spawn_points[0] as Vector3)
	var ent: Vector3 = game.level_info.entrance.position
	_look_from(anchor + (anchor - ent).normalized() * 9.0 + Vector3(-6.0, 0, 0), ent + Vector3(0, 2.0, 0))
	return true


func _pose_neutral_doors() -> bool:
	var n: Dictionary = game.level_info.get("neutral", {})
	if n.is_empty():
		return false
	var ent: Vector3 = game.level_info.entrance.position
	_look_from(ent + Vector3(0, 0, 2.2), n.shop.position + Vector3(0, 1.0, 0))
	return true


func _room(kind: String) -> Dictionary:
	for r in game.level_info.get("rooms", []):
		if r.kind == kind:
			return r
	return {}


func _pose_lobby() -> bool:
	var r := _room("lobby")
	if r.is_empty():
		return false
	var rect: Rect2 = r.rect
	_look_from(Vector3(rect.end.x - 1.0, 0, rect.end.y - 1.0), Vector3(rect.position.x + 3.0, 1.2, rect.position.y + 1.0))
	return true


func _pose_hall() -> bool:
	var er: Rect2 = game.level_info.get("entrance_rect", Rect2())
	if er.size == Vector2.ZERO:
		return false
	_look_from(Vector3(er.position.x + 2.0, 0, er.position.y + 3.2), Vector3(er.end.x - 2.0, 1.4, er.position.y + 3.0))
	return true


func _pose_break_room() -> bool:
	var r := _room("break_room")
	if r.is_empty():
		return false
	var rect: Rect2 = r.rect
	_look_from(Vector3(rect.end.x - 0.9, 0, rect.end.y - 0.9), Vector3(rect.position.x + 1.0, 1.0, rect.position.y + rect.size.y * 0.45))
	return true


func _pose_or() -> bool:
	var r := _room("or")
	if r.is_empty():
		return false
	var rect: Rect2 = r.rect
	var tables: Array = game.level_info.get("tables", [])
	var c := Vector3.ZERO
	for t in tables:
		c += t.position
	c /= maxf(1.0, tables.size())
	_look_from(Vector3(rect.position.x + 1.0, 0, rect.end.y - 0.9), c + Vector3(0, 0.8, -1.0))
	return true


## Stand at tile `from` of the entrance building's plan and look at tile `at`, `h` metres up.
func _pose_hub(from: Vector2, at: Vector2, h: float) -> bool:
	var er: Rect2 = game.level_info.get("entrance_rect", Rect2())
	if er.size == Vector2.ZERO:
		return false
	var f := er.position + from * C.TILE
	var t := er.position + at * C.TILE
	_look_from(Vector3(f.x, 0, f.y), Vector3(t.x, h, t.y))
	return true


## SHOWERS (2026-09-24): standing right in front of the first shower with its water running, so the
## stream and the floor mist are unmistakably in frame (not a wide shot where they'd read as noise).
func _pose_hub_shower_on() -> bool:
	var showers := game.level.find_child("Showers", true, false) if game.level != null else null
	if showers == null or (showers.get("_showers") as Array).is_empty():
		print("[hospitalshot] no Showers node on this level")
		return false
	showers.set_on(0, true)
	var sh: Node3D = showers.get("_showers")[0]
	var out: Vector3 = -sh.global_basis.z.normalized()
	_look_from(sh.global_position + out * 2.2 + Vector3(0, 0.2, 0), sh.global_position + Vector3(0, 1.1, 0))
	# The torch this close blows the stream out white; the shower's own doorway light is enough.
	if bot.has_method("set_flashlight"):
		bot.set_flashlight(false)
	# Extra settle so the stream is mid-flow (not just its first frame or two of drops) by the time
	# the caller's own SETTLE_FRAMES wait takes the shot.
	for i in 40:
		await get_tree().process_frame
	return true


## The pharmacy's fax order form, open, with pills ticked.
func _pose_fax_form() -> bool:
	game.add_money(100, "shot")
	game.economy.open_fax_ui()
	await get_tree().process_frame
	var ui = game.economy.fax_ui
	if ui._rows.is_empty():
		return false
	ui._rows[0].box.checked = true
	ui._rows[0].box.queue_redraw()
	return true


## Light leak checks: the projector on, the flashlight off, looking at the wall a neighbour's light
## used to shine through.
func _pose_hub_leak(from: Vector2, at: Vector2, h: float, projector: bool) -> bool:
	if projector:
		game._set_projector(true)
	if bot.has_method("set_flashlight"):
		bot.set_flashlight(false)
	return _pose_hub(from, at, h)


## The pickup drawer pulled out through its slot, seen from the lobby at an angle.
func _pose_drawer() -> bool:
	if game.economy.fax_ui_open():
		game.economy.fax_ui.close()
	var ph: Node3D = game.economy.pharmacy
	ph._drawer_state = "open"
	ph._drawer_k = 1.0
	ph._apply_drawer()
	var at: Vector3 = ph.global_transform * Vector3(0.9, 1.5, 1.7)
	_look_from(Vector3(at.x, 0.0, at.z), ph.global_transform * Vector3(0.0, 1.0, 0.2))
	return true


## The lobby's fax desk, from where a player stands to use it.
func _pose_fax_desk() -> bool:
	game.economy.fax_ui.close()
	var t: Node3D = game.economy.pharmacy.terminal
	var at: Vector3 = t.global_transform * Vector3(0.2, 0.0, 1.1)
	_look_from(at, t.global_transform * Vector3(0.15, 1.0, 0.0))
	return true


## The attendant at the fax machine, reading the page in her hand.
func _pose_nurse_reading() -> bool:
	game.economy.fax_ui.close()
	var ph: Node3D = game.economy.pharmacy
	ph._nurse.position = ph._at_fax
	ph._start("read")
	ph._nurse.get("nurse").hold = 1.0
	for i in 5:
		await get_tree().process_frame
	var at: Vector3 = ph.global_transform * (ph._at_fax + Vector3(-0.9, 0.0, 1.9))
	_look_from(Vector3(at.x, 0.0, at.z), ph.global_transform * (ph._at_fax + Vector3(0, 1.4, 0)))
	return true


## Looking at the waiting room's seated Night Nurse from a few metres in front of her.
func _pose_waiting_nurse(offset: Vector3) -> bool:
	var n: Node3D = game.economy.waiting_nurse
	if n == null:
		return false
	var front: Vector3 = n.global_position + n.global_basis * offset
	_look_from(Vector3(front.x, n.global_position.y + 0.5, front.z), n.global_position + Vector3.UP * 1.0)
	return true


## Close up on the crematorium's furnace window with its hatch open, from off to one side.
func _pose_furnace_open() -> bool:
	var furn: Node3D = game.economy.furnace
	if furn == null:
		return false
	furn.set_hatch(true, false)
	var front: Vector3 = furn.global_transform * Vector3(-1.4, 0, 3.6)
	_look_from(front, furn.global_transform * Vector3(0, 1.4, -1.0))
	return true


func _pose_hallway() -> bool:
	# The monster spawn with the longest clear view down a hallway.
	var space := bot.get_world_3d().direct_space_state
	var best_len := -1.0
	var best_from := Vector3.ZERO
	var best_dir := Vector3.FORWARD
	for spot in game.level_info.get("monster_spawns", []):
		var eye: Vector3 = spot + Vector3.UP * C.EYE_H
		for i in 4:
			var dir := Vector3(cos(TAU * i / 4.0), 0, sin(TAU * i / 4.0))
			var q := PhysicsRayQueryParameters3D.create(eye, eye + dir * 80.0)
			q.collision_mask = C.L_WORLD
			var hit := space.intersect_ray(q)
			var reach: float = 80.0 if hit.is_empty() else eye.distance_to(hit.position)
			if reach > best_len:
				best_len = reach
				best_from = spot
				best_dir = dir
	if best_len < 0.0:
		return false
	print("[hospitalshot] hallway sightline %.1f m" % best_len)
	_look_from(best_from, best_from + best_dir * 20.0 + Vector3.UP * 1.5)
	return true


func _pose_room(kind: String) -> bool:
	var r := _room(kind)
	if r.is_empty():
		return false
	var rect: Rect2 = r.rect
	var doors: Array = r.doors
	var centre := Vector3(rect.get_center().x, 0, rect.get_center().y)
	if doors.is_empty():
		_look_from(centre, centre + Vector3(1, 1.0, 0))
		return true
	var door: Vector3 = doors[0]
	# Stand just outside the doorway, looking across the room to its far side.
	var into := (centre - door)
	into.y = 0.0
	var dir := Vector3(signf(into.x), 0, 0) if absf(into.x) > absf(into.z) else Vector3(0, 0, signf(into.z))
	var far := door + dir * (rect.size.x if dir.x != 0.0 else rect.size.y)
	_look_from(door - dir * 1.1, (far + centre) * 0.5 + Vector3.UP * 0.9)
	return true
